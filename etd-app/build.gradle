import br.com.arndroid.buildutils.BuildUtils

apply plugin: 'com.android.application'

android {

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            // To see warnings details uncomment the following line:
            // options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    gradle.buildFinished { buildResult ->
        println BuildUtils.buildSummaryFor ('etd-app', defaultVersionName, defaultVersionCode, buildResult.failure)
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def newApkFileName = "$project.buildDir/outputs/apk/" +
                        BuildUtils.getOutputFileNameFor('ETD-APP', '.apk', defaultVersionName, variant.baseName)
                output.outputFile = new File(newApkFileName)
            }
        }
    }

    compileSdkVersion defaultCompileSdkVersion
    buildToolsVersion defaultBuildToolsVersion

    defaultConfig {
        applicationId 'br.com.arndroid.etdiet'
        minSdkVersion defaultMinSdkVersion
        targetSdkVersion defaultTargetSdkVersion
        versionName defaultVersionName
        versionCode defaultVersionCode
    }

    signingConfigs {
        release {
            storeFile file(defaultStoreFile)
            storePassword defaultStorePassword
            keyAlias defaultKeyAlias
            keyPassword defaultKeyPassword
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }

        release {
            minifyEnabled false
            // noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile project(':etd-shr')
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:support-v4:22.1.1'
    compile group: 'com.github.tony19', name: 'logback-android-core', version: '1.0.10-2'
    compile group: 'com.github.tony19', name: 'logback-android-classic', version: '1.0.10-2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
    androidTestCompile group: 'com.google.dexmaker', name: 'dexmaker', version: '1.2'
    androidTestCompile group: 'com.google.dexmaker', name: 'dexmaker-mockito', version: '1.2'
    androidTestCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
}