import br.com.arndroid.buildutlis.AppBuildProfile

apply plugin: 'com.android.application'

android {

    AppBuildProfile buildProfile = new AppBuildProfile('etd-app')

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            // To see warnings details uncomment the following line:
            // options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //noinspection GroovyAssignabilityCheck
                def newApkFileName = "$project.buildDir/outputs/apk/" + buildProfile.getOutputFileNameForVariant(variant.baseName)
                output.outputFile = new File(newApkFileName)
            }
        }
    }

    compileSdkVersion buildProfile.getCompileSdkVersion()
    buildToolsVersion buildProfile.getBuildToolsVersion()

    defaultConfig {
        applicationId 'br.com.arndroid.etdiet'
        minSdkVersion buildProfile.getMinSdkVersion()
        targetSdkVersion buildProfile.getTargetSdkVersion()
        versionName buildProfile.getVersionName()
        versionCode buildProfile.getVersionCode()
    }

    signingConfigs {
        release {
            storeFile file(buildProfile.getStoreFile())
            storePassword buildProfile.getStorePassword()
            keyAlias buildProfile.getKeyAlias()
            keyPassword buildProfile.getStorePassword()
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }

        release {
            minifyEnabled false
            // noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile project(':etd-shr')
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.android.support:support-v4:22.1.1'
    compile group: 'com.github.tony19', name: 'logback-android-core', version: '1.0.10-2'
    compile group: 'com.github.tony19', name: 'logback-android-classic', version: '1.0.10-2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
    androidTestCompile group: 'com.google.dexmaker', name: 'dexmaker', version: '1.0'
    androidTestCompile group: 'com.google.dexmaker', name: 'dexmaker-mockito', version: '1.0'
    androidTestCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
}