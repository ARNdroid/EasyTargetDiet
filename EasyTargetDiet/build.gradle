buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.3'
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

android {
    ETDBuildProfile buildProfile = new ETDBuildProfile()

    android.applicationVariants.all { variant ->
        // Change outputFile name for all variants
        // The pattern is 'EasyTargetDiet-major_minor_hotfix-VARIANT.apk'
        //noinspection GroovyAssignabilityCheck
        variant.outputFile = file("$project.buildDir/outputs/apk/" + buildProfile.getAPKNameForVariant(variant.baseName))
    }

    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    defaultConfig {
        applicationId 'br.com.arndroid.etdiet'
        minSdkVersion 15
        targetSdkVersion 21
        versionName buildProfile.getVersionName()
        versionCode buildProfile.getVersionCode()
    }

    signingConfigs {
        release {
            storeFile file(buildProfile.getStoreFile())
            storePassword buildProfile.getStorePassword()
            keyAlias buildProfile.getKeyAlias()
            keyPassword buildProfile.getStorePassword()
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }

        release {
            // noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.android.support:support-v4:21.0.0'
    compile group: 'com.github.tony19', name: 'logback-android-core', version: '1.0.10-2'
    compile group: 'com.github.tony19', name: 'logback-android-classic', version: '1.0.10-2'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
}

class ETDBuildProfile {
    private int major
    private int minor
    private int hotfix
    private String storeFile
    private String storePassword
    private String keyAlias

    public ETDBuildProfile() {
        getPropertiesFromFile()
        validatePropertiesOrThrow()
        confirmPropertiesWithUserOrThrow()
        getPropertiesFromUser()
    }

    public void getPropertiesFromFile() {
        // /gradle.properties
        Properties properties = new Properties()
        properties.load(new FileInputStream(System.getProperty("user.dir") + '/EasyTargetDiet/gradle.properties'))
        major = Integer.parseInt(properties.getProperty('major'))
        minor = Integer.parseInt(properties.getProperty('minor'))
        hotfix = Integer.parseInt(properties.getProperty('hotfix'))

        // /local.properties
        properties.load(new FileInputStream(System.getProperty("user.dir") + '/EasyTargetDiet/local.properties'))
        storeFile = properties.getProperty('store_file')
        keyAlias = properties.getProperty('key_alias')
    }

    public void validatePropertiesOrThrow() {
        if (major < 0) {
            throw new InvalidUserDataException("Version major = ${major}. Must be a positive integer")
        }
        if (minor < 0 || minor > 99) {
            throw new InvalidUserDataException("Version minor = ${minor}. Must be between 0 and 99.")
        }
        if (hotfix < 0 || hotfix > 999) {
            throw new InvalidUserDataException("Version hotfix = ${hotfix}. Must be between 0 and 999.")
        }
    }

    public void confirmPropertiesWithUserOrThrow() {
        // Current version
        //System.out.println "ETD ==> Current version is ${getVersionName()} (${getVersionCode()})"
        String answer = getUserEntry("\nETD ==> Current version is ${getVersionName()} (${getVersionCode()})\nETD ==> Enter yEs to accept this version: ")
        if (answer == null) {
            System.out.println("\nETD ==> Version ${getVersionName()} (${getVersionCode()}) accepted by default.")
            answer = 'yEs'
        }
        if (!'yEs'.equals(answer)) {
            System.out.println "ETD ==> User entered '${answer}'. Build will fail (stop)."
            throw new InvalidUserDataException("Current version ${getVersionName()} (${getVersionCode()}) NOT accepted.");
        }
    }

    public void getPropertiesFromUser() {
        storePassword = getUserEntry('\nETD ==> Enter storePassword: ')
    }

    int getVersionCode() {
        return 100000 * major + 1000 * minor + hotfix
    }

    public String getVersionName() {
        return "${major}.${minor}.${hotfix}"
    }

    public String getVersionNameForAPK() {
        return "${major}_${minor}_${hotfix}"
    }

    public String getStoreFile() {
        return storeFile
    }

    public String getKeyAlias() {
        return keyAlias
    }

    public String getStorePassword() {
        return storePassword
    }

    public String getAPKNameForVariant(String variantName) {
        return "EasyTargetDiet-${getVersionNameForAPK()}-${variantName.toUpperCase()}.apk"
    }

    private static String getUserEntry(String message) {
        String userEntry
        try {
            userEntry = System.console().readLine(message)
        }
        catch(Exception e) {
            // We know... catching Exception is not so good,
            // but it's enough here.
            StringBuilder builder = new StringBuilder()
            builder.append('\nETD ==> When getting following user entry:')
            builder.append('\nETD ==> ----------------------------------')
            builder.append(message)
            builder.append('\nETD ==> ----------------------------------')
            builder.append('\nETD ==> This exception occurred:')
            builder.append('\nETD ==> ----------------------------------')
            builder.append("\nETD ==> ${e}")
            builder.append('\nETD ==> ----------------------------------')
            builder.append('\nETD ==> Null will be returned')
            System.out.println(builder.toString())
            return null
        }
        return userEntry
    }
}